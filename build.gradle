buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.spring.gradle:dependency-management-plugin:${dependencyManagementVersion}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = "2.14"
}

configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'
    apply from: '../libraries.gradle'

    buildDir = "build"
    group 'ru.romanow.core'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyManagement {
        imports {
            mavenBom "io.spring.platform:platform-bom:${springPlatformVersion}"
        }
    }

    ext['gson.version'] = "${gsonVersion}"
    ext['guava.version'] = "${guavaVersion}"

    repositories {
        mavenCentral()
    }
}

project("rest-client") {
    version = "${restClientVersion}"

    dependencies {
        compile "org.springframework:spring-core"
        compile "org.springframework:spring-web"

        compile "org.springframework.boot:spring-boot-starter-logging"

        compile "com.google.code.gson:gson"

        compileOnly "com.google.code.findbugs:jsr305:${findBugsVersion}"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}

project("commons") {
    version = "${commonsVersion}"

    dependencies {
        compile "org.springframework:spring-core"
        compile "org.springframework:spring-web"

        compile "org.springframework:spring-context"
        compile "org.springframework:spring-aspects"

        compile "org.springframework.boot:spring-boot-starter-logging"

        compile "com.google.code.gson:gson"
        compile "com.google.guava:guava"
        compile "commons-lang:commons-lang"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}
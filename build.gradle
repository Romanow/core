configure(subprojects) { project ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply from: '../libraries.gradle'

    buildDir = "build"
    group 'ru.romanow.core'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    jar {
        archiveName =  "${project.name}.jar"
    }

    repositories {
        mavenCentral()
    }
    
    dependencies {
        compile "org.slf4j:slf4j-api:${slf4jVersion}"
        compile "com.google.code.findbugs:jsr305:${findBugsVersion}"
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
    }
}

project("rest-client") {
    version = "${restClientVersion}"

    dependencies {
        compile "org.apache.httpcomponents:httpasyncclient:${apacheHttpClient}"
        compile "com.google.code.gson:gson:${gsonVersion}"

        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }
}

project("commons") {
    version = "${commonsVersion}"

    dependencies {
        compile "org.springframework:spring-web:${springVersion}"
        compile "org.springframework:spring-context:${springVersion}"
        compile "org.springframework:spring-aspects:${springVersion}"

        compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
        compile "com.google.code.gson:gson:${gsonVersion}"
        compile "com.google.guava:guava:${guavaVersion}"

        testCompile "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
    }
}